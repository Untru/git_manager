#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция НастройкиБазы(База) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Базы.Ссылка КАК База,
		|	Базы.Наименование КАК Наименование,
		|	Базы.КластерСерверов КАК КластерСерверов,
		|	Базы.ИмяИнформационнойБазы КАК ИмяИнформационнойБазы,
		|	Базы.НомерВерсииПлатформы КАК НомерВерсииПлатформы,
		|	Базы.ИмяПользователяИнформационнойБазы КАК ИмяПользователяИнформационнойБазы,
		|	Базы.ПарольПользователяИнформационнойБазы КАК ПарольПользователяИнформационнойБазы,
		|	ПРЕДСТАВЛЕНИЕ(Базы.ТипSQL) КАК ТипSQL,
		|	Базы.ИмяSQL КАК ИмяSQL,
		|	Базы.ЛогинSQL КАК ЛогинSQL,
		|	Базы.ПарольSQL КАК ПарольSQL,
		|	Базы.КаталогRAS КАК КаталогRAS,
		|	Проекты.URLGitLab КАК URLGitLab,
		|	Проекты.ИДПроектаGitLab КАК ИДПроектаGitLab,
		|	Проекты.PathGitClone КАК PathGitClone,
		|	Проекты.РежимРаботыКонфигурации КАК РежимРаботыКонфигурации
		|ИЗ
		|	Справочник.Базы КАК Базы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Проекты КАК Проекты
		|		ПО Базы.Владелец = Проекты.Ссылка
		|ГДЕ
		|	Базы.Ссылка = &База";
	
	Запрос.УстановитьПараметр("База", База);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Возврат ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(РезультатЗапроса.Выгрузить()[0]);	
	Иначе
		Возврат Новый Структура;
	КонецЕсли;
	
КонецФункции

Функция БазаТекущегоПользователя() Экспорт
	
	БазаТекущегоПользователя = ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	Базы.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.Базы КАК Базы
	               |ГДЕ
	               |	Базы.Ответственный = &Ответственный";
	
	Запрос.УстановитьПараметр("Ответственный",ПараметрыСеанса.ТекущийПользователь);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		БазаТекущегоПользователя = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат БазаТекущегоПользователя;
	
КонецФункции

#КонецОбласти

#КонецЕсли

